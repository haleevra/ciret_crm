
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СделкиСКлиентами.Ответственный КАК КАМ,
	|	СделкиСКлиентами.Партнер КАК Клиент,
	|	ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи КАК ВидВстречи,
	|	ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.НомерСтроки КАК Порядок,
	|	ВЫБОР
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.НоваяСделка)
	|			ТОГДА 10
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ПервичныйКонтакт)
	|			ТОГДА 10
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ФормированиеПредложения)
	|			ТОГДА 5
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.СогласованиеУсловий)
	|			ТОГДА 15
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ВыполнениеОбязательств)
	|			ТОГДА 10
	|	КОНЕЦ КАК Срок,
	|	ЛОЖЬ КАК Рассмотрено,
	|	ВЫБОР
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.НоваяСделка)
	|			ТОГДА СделкиСКлиентами.ДатаНачала
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ПервичныйКонтакт)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 10)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ФормированиеПредложения)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 20)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.СогласованиеУсловий)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 25)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ВыполнениеОбязательств)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 40)
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.НоваяСделка)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 10)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ПервичныйКонтакт)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 20)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ФормированиеПредложения)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 25)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.СогласованиеУсловий)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 40)
	|		КОГДА ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.ЭтапПроцессаПродажи = ЗНАЧЕНИЕ(Справочник.СостоянияПроцессов.ВыполнениеОбязательств)
	|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СделкиСКлиентами.ДатаНачала, ДЕНЬ), ДЕНЬ, 50)
	|	КОНЕЦ КАК ДатаОкончания,
	|	0 КАК Длительность,
	|	СделкиСКлиентами.Ссылка
	|ПОМЕСТИТЬ ВТ_Сделки
	|ИЗ
	|	Справочник.ВидыСделокСКлиентами.ЭтапыСделкиПоПродаже КАК ВидыСделокСКлиентамиЭтапыСделкиПоПродаже
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СделкиСКлиентами КАК СделкиСКлиентами
	|		ПО ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.Ссылка = СделкиСКлиентами.ВидСделки
	|ГДЕ
	|	ВидыСделокСКлиентамиЭтапыСделкиПоПродаже.Ссылка.Наименование = ""ПК""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Сделки.КАМ КАК КАМ,
	|	ВТ_Сделки.Клиент КАК Клиент,
	|	ВТ_Сделки.ВидВстречи КАК ВидВстречи,
	|	ВТ_Сделки.Порядок,
	|	ВТ_Сделки.Срок,
	|	ВЫБОР
	|		КОГДА СтатистикаСделокСКлиентами.Продолжительность > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Рассмотрено,
	|	ЕСТЬNULL(СтатистикаСделокСКлиентами.ДатаНачала, ВТ_Сделки.ДатаНачала) КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА СтатистикаСделокСКлиентами.ДатаОкончания ЕСТЬ NULL
	|			ТОГДА ВТ_Сделки.ДатаОкончания
	|		КОГДА СтатистикаСделокСКлиентами.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ВТ_Сделки.ДатаОкончания
	|		ИНАЧЕ СтатистикаСделокСКлиентами.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА СтатистикаСделокСКлиентами.ДатаОкончания ЕСТЬ NULL
	|			ТОГДА ВТ_Сделки.Длительность
	|		ИНАЧЕ РАЗНОСТЬДАТ(СтатистикаСделокСКлиентами.ДатаНачала, СтатистикаСделокСКлиентами.ДатаОкончания, ДЕНЬ)
	|	КОНЕЦ КАК Длительность,
	|	ВТ_Сделки.Ссылка
	|ПОМЕСТИТЬ ВТ_СтатистикаСделокСКлиентами
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатистикаСделокСКлиентами КАК СтатистикаСделокСКлиентами
	|		ПО ВТ_Сделки.Ссылка = СтатистикаСделокСКлиентами.Сделка
	|			И ВТ_Сделки.ВидВстречи = СтатистикаСделокСКлиентами.ЭтапПроцесса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_СтатистикаСделокСКлиентами.КАМ,
	|	ВТ_СтатистикаСделокСКлиентами.Клиент КАК Клиент,
	|	ВТ_СтатистикаСделокСКлиентами.ВидВстречи,
	|	ВТ_СтатистикаСделокСКлиентами.Порядок КАК Порядок,
	|	ВТ_СтатистикаСделокСКлиентами.Срок КАК Срок,
	|	ВТ_СтатистикаСделокСКлиентами.Рассмотрено,
	|	ВТ_СтатистикаСделокСКлиентами.ДатаНачала КАК ДатаНачала,
	|	ВТ_СтатистикаСделокСКлиентами.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_СтатистикаСделокСКлиентами.Длительность КАК Длительность,
	|	ВТ_СтатистикаСделокСКлиентами.Ссылка
	|ИЗ
	|	ВТ_СтатистикаСделокСКлиентами КАК ВТ_СтатистикаСделокСКлиентами
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &КАМ = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВТ_СтатистикаСделокСКлиентами.КАМ = &КАМ
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВТ_СтатистикаСделокСКлиентами.ДатаНачала >= &ДатаНачала
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Клиент,
	|	Порядок
	|ИТОГИ
	|	СУММА(Срок),
	|	МИНИМУМ(ДатаНачала),
	|	МАКСИМУМ(ДатаОкончания),
	|	СУММА(Длительность)
	|ПО
	|	Клиент"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КАМ", КАМ);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);

	СформироватьНаСервереДиаграммуГанта(Запрос);
	//СформироватьНаСервереТабличныйДокумент(Запрос);
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервереДиаграммуГанта(Запрос)
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Клиент");
	
	ДиаграммаГанта.Очистить();
	ДиаграммаГанта.Обновление = Ложь;
	
	Пока Выборка.Следующий() Цикл
		Родитель = Неопределено;
		Серия = ДиаграммаГанта.УстановитьСерию(Выборка.Клиент);
		Точка = ДиаграммаГанта.УстановитьТочку(Выборка.Клиент);
		Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Начало = Выборка.ДатаНачала;
		Интервал.Конец = Выборка.ДатаОкончания;
		УстановитьЦветИнтервала(Выборка, Интервал, Ложь);
		ВыборкаДетальныеЗаписи = Выборка.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Точка = ДиаграммаГанта.УстановитьТочку(ВыборкаДетальныеЗаписи.ВидВстречи, Выборка.Клиент);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			Интервал = Значение.Добавить();
			Интервал.Начало = ВыборкаДетальныеЗаписи.ДатаНачала;
			Интервал.Конец = ВыборкаДетальныеЗаписи.ДатаОкончания;
			Интервал.Расшифровка = ВыборкаДетальныеЗаписи.Ссылка;
			УстановитьЦветИнтервала(ВыборкаДетальныеЗаписи, Интервал);
			Если Не Родитель = Неопределено Тогда
				Родитель.Добавить(Интервал);
			КонецЕсли;
			Родитель = Интервал;
		КонецЦикла;
	КонецЦикла;
	
	ДиаграммаГанта.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦветИнтервала(Выборка, Интервал, Желтый = Истина)
	
	Если Выборка.Длительность > Выборка.Срок Тогда
		Интервал.Цвет = WebЦвета.Красный;
	Иначе
		Если ТекущаяДата() < Выборка.ДатаОкончания И Желтый Тогда
			Интервал.Цвет = WebЦвета.Желтый;
		Иначе
			Интервал.Цвет = WebЦвета.Зеленый;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого ЭлементШкалы Из ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы Цикл
		Сегодня = ЭлементШкалы.Метки.Добавить(ТекущаяДата());
		Сегодня.Текст = "Сегодня";
		Сегодня.ЦветЛинии = WebЦвета.Красный;
		Прервать;
	КонецЦикла;
КонецПроцедуры
